defaultTasks 'clean', 'check', 'assemble'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'
apply plugin: "com.gradle.plugin-publish"

group = 'org.jsweet'
version = '2.2.0-SNAPSHOT'
description = 'JSweet Gradle plugin'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

buildscript {
	dependencies {
		classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
		classpath 'junit:junit:4.12'
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
	}
	repositories {
		maven { url "http://repository.jboss.org/nexus/content/groups/public/" }
		mavenLocal()
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

repositories {
	jcenter()
	mavenCentral()

	maven { url "http://repository.jsweet.org/artifactory/libs-release-local" }
	maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }
}

dependencies {

	compile gradleApi()
	compile localGroovy()

	compile group: 'org.jsweet', name: 'jsweet-transpiler', version:'2.2.0-SNAPSHOT'
	compile group: 'org.jsweet', name: 'jsweet-core', version:'6-SNAPSHOT'
	compile group: 'org.codehaus.plexus', name: 'plexus-utils', version:'3.0.20'
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

publishing {
	repositories {
		maven {
			credentials {
        		username "$mavenUser"
        		password "$mavenPassword"
	        }
			name "jsweet-snapshots"
			url "http://jsweet.org:8081/artifactory/plugins-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release' }-local"
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = 'org.jsweet'
			artifactId = 'jsweet-gradle-plugin'

			pom.withXml {
				asNode().appendNode('description',
						'Gradle plugin for JSweet')
			}

			from components.java

			artifact sourceJar { classifier "sources" }
		}
	}
}

pluginBundle {
  website = 'http://www.jsweet.org/'
  vcsUrl = 'https://github.com/cincheo/jsweet'
  description = 'JSweet Gradle plugin'
  tags = ['jsweet', 'transpiler', 'java', 'web', 'typescript', 'typescript', 'gradle']

  plugins {
    jsweetPlugin {
      id = 'org.jsweet.jsweet-gradle-plugin'
      displayName = 'JSweet Gradle plugin'
    }
  }
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}
